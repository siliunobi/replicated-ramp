load replica-table

mod RAMP-F is
  including CLIENT-REPLICA .
  including MESSAGE .
  including REPLICA-TABLE .
  including SAMPLER .
  including VERSION-RAMP .
  including VALUES-NAT .


  op ld : -> Delay .  --- local delay 
  op rd : -> Delay .  --- remote delay
  op nd : -> Delay .  --- no delay
  eq nd = 0.0 .

  --- lognormal distribution
  eq ld = sampleLogNormal(0.0,1.0) . --- miu, sigma 
  eq rd = sampleLogNormal(0.0,1.0) * 100.0 .

  --- weibull distribution
---   eq ld = sampleWeibull(5.0,1.0) . 
---   eq rd = sampleWeibull(3.0,300.0) .

  --- or, ld = 0.0
  --- in our model, local delay seems to be local "processing" delay,
  --- which is negligible compared to the distributed data communication
  --- thus, the settings in our experiments could be:
---  eq ld = 0.0 .
---  eq rd = sampleLogNormal(0.0,1.0) .
--- eq rd = sampleWeibull(5.0,1.0) .


  vars RID TID RID' TABLE M OID OID' A : Address .
  vars RIDS RIDS' : AddressSet .
  vars AS AS' AS'' : AttributeSet .
  vars TXNS TXNS' : ConfigList .
  var K : Key .
  var X : LocalVar .
  var REPLICA-TABLE : ReplicaTable .
  vars OPS OPS' OPS'' : OperationList .
  vars V V' : Value .
  var WS : WriteSet .
  vars VARS VARS' : LocalVars .
  vars RS DS RS' WRITES WRITES' READS : Versions .
  vars LC VL VL' : KeyTimestamps .
  var VERSION : Version .
  vars TS TS' : Timestamp .
  vars MD MD' KS : KeySet .
  var VS : Versions .
***
  vars SQN SQN' : Float .
  var EXPR : Expression .
  var RT : KeyReplicas .
  vars 1STGETS 1STGETS' : 1stGets .
  vars 2NDGETS 2NDGETS' : 2ndGets .
  vars VSTS CMTS VSTS' CMTS' : VoteSites .
  vars T T1 : Float .
  var FLAG : Bool .
  vars RECORD RECORD' : Record .
  var DR : DcReplicas .
  vars DC DC' : Address .
  var KR : KeyReplicas .
  vars RIDSS RIDSS' : AddressList .
  var CONT : Content .

  *****************
  *** Sticky HA ***
  *****************

  ---??? simply consume "start" if no txn needs to start
  rl [receiving-start-when-no-txn-to-start] :
     < A : Client | gotTxns: emptyTxnList, AS >
     {T, A <- start}
   =>
     < A : Client | gotTxns: emptyTxnList, AS > .


  --- "gotTxns" abstracts "client" away
 crl [start-ro-or-rw-txn] :
     {T, A <- start}  
     < M : Monitor | log: RECORD >
     < TABLE : Table | table: REPLICA-TABLE, datacenter: DR >
     < A : Client | gotTxns: (< TID : Txn | operations: OPS,
                                            latest: empty,
											1stGetSites: 1STGETS, AS > ;; TXNS),
                    executing: noActor,
					dc: DC, AS' >
   =>
     < M : Monitor | log: (RECORD ; record(TID,T,0.0,empty,empty,false)) >
     < TABLE : Table | table: REPLICA-TABLE, datacenter: DR >
     < A : Client | gotTxns: TXNS,
                    executing: < TID : Txn | operations: OPS,
                                             latest: vl(OPS),
											 1stGetSites: (1STGETS ; 1st(TID,add1st(OPS,REPLICA-TABLE,DR,DC))), AS >,  --- initialize "v_latest"
					dc: DC, AS' >
     genGets(A,TID,OPS,REPLICA-TABLE,DR,DC) 
     if (not write-only(OPS)) .
	 
  op write-only : OperationList -> Bool .
  eq write-only((OPS (OID X :=read K) OPS')) = false .
  eq write-only(OPS) = true [owise] .
  
  *** sticky: to local datacenter replica  
  op add1st : OperationList ReplicaTable DcReplicas Address -> AddressSet .
  eq add1st(((OID X :=read K) OPS),[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC) =
       RID ; add1st(OPS,[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC) .
  eq add1st((write(OID,K,EXPR) OPS),[KR],DR,DC) = add1st(OPS,[KR],DR,DC) .
  eq add1st(nil,[KR],DR,DC) = empty .
  
  op genGets : Address Address OperationList ReplicaTable DcReplicas Address -> Config .
  eq genGets(A,TID,((OID X :=read K) OPS),[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC) = 
       genGets(A,TID,OPS,[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC)
       [ld, RID <- get(TID,K,eptTS,DC,A)] .  --- eptTS denotes "empty" in line 25
  eq genGets(A,TID,(write(OID,K,EXPR) OPS),[KR],DR,DC) =
       genGets(A,TID,OPS,[KR],DR,DC) .
  eq genGets(A,TID,nil,[KR],DR,DC) = null .

  op vl : OperationList -> KeyTimestamps .
  eq vl((write(OID,K,EXPR) OPS)) = vl(OPS) .
  eq vl(((OID X :=read K) OPS)) = K |-> ts(-1,-1.0), vl(OPS) .
  eq vl(nil) = empty .

 crl [start-wo-txn] :
     {T, A <- start}  
     < M : Monitor | log: RECORD >
     < TABLE : Table | table: REPLICA-TABLE, datacenter: DR >
     < A : Client | gotTxns: (< TID : Txn | operations: OPS,
                                            localVars: VARS, 
											txnSqn: SQN, 
											voteSites: VSTS, AS > ;; TXNS),
                    executing: noActor,
	        		dc: DC, AS' >
   =>
     < M : Monitor | log: (RECORD ; record(TID,T,0.0,empty,WRITES,false)) >
     < TABLE : Table | table: REPLICA-TABLE, datacenter: DR >
     < A : Client | gotTxns: TXNS,
                    executing: < TID : Txn | operations: OPS,
                                             localVars: VARS,
											 txnSqn: T,
											 voteSites: (VSTS ; voteSites(TID,addVotes(OPS,REPLICA-TABLE,DR,DC))), AS >,
	    			dc: DC, AS' >
     genPuts(A,TID,OPS,T,VARS,REPLICA-TABLE,DR,DC)
     if write-only(OPS) /\
        WRITES := ws(OPS,A,T,VARS) .

  op addVotes : OperationList ReplicaTable DcReplicas Address -> AddressSet .
  eq addVotes((write(OID,K,EXPR) OPS),[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC) =
       RID ; addVotes(OPS,[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC) .
  eq addVotes(((OID X :=read K) OPS),[KR],DR,DC) = addVotes(OPS,[KR],DR,DC) .
  eq addVotes(nil,[KR],DR,DC) = empty .  
  
  op genPuts : Address Address OperationList Float LocalVars ReplicaTable DcReplicas Address -> Config .	
  op $genPuts : Address Address OperationList Float LocalVars ReplicaTable DcReplicas Address OperationList -> Config .	
  eq genPuts(A,TID,OPS,SQN,VARS,[KR],DR,DC) = $genPuts(A,TID,OPS,SQN,VARS,[KR],DR,DC,OPS) .
  eq $genPuts(A,TID,(write(OID,K,EXPR) OPS),SQN,VARS,[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC,(OPS' write(OID,K,EXPR) OPS'')) =
       $genPuts(A,TID,OPS,SQN,VARS,[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC,(OPS' write(OID,K,EXPR) OPS''))
       [ld, RID <- prepare(TID,version(K,eval(EXPR,VARS),ts(A,SQN),md(OPS' OPS'')),DC,A)] .
  eq $genPuts(A,TID,((OID X :=read K) OPS),SQN,VARS,[KR],DR,DC,OPS') =
       $genPuts(A,TID,OPS,SQN,VARS,[KR],DR,DC,OPS') .
  eq $genPuts(A,TID,nil,SQN,VARS,[KR],DR,DC,OPS') = null .
  
  op ws : OperationList Address Float LocalVars -> Versions .
  op $ws : OperationList Address Float LocalVars OperationList -> Versions .
  eq ws(OPS,RID,SQN,VARS) = $ws(OPS,RID,SQN,VARS,OPS) .
  eq $ws((write(OID,K,EXPR) OPS),RID,SQN,VARS,(OPS' write(OID,K,EXPR) OPS'')) = 
       version(K,eval(EXPR,VARS),ts(RID,SQN),md(OPS' OPS'')), 
       $ws(OPS,RID,SQN,VARS,(OPS' write(OID,K,EXPR) OPS'')) .
  eq $ws(((OID X :=read K) OPS),RID,SQN,VARS,OPS') =
       $ws(OPS,RID,SQN,VARS,OPS') .
  eq $ws(nil,RID,SQN,VARS,OPS') = empty .	
  
  op md : OperationList -> KeySet .
  eq md((write(OID,K,EXPR) OPS)) = K, md(OPS) .
  eq md(((OID X :=read K) OPS)) = md(OPS) .
  eq md(nil) = empty .	
  
	
  rl [receive-prepare] :
     < TABLE : Table | datacenter: DR, AS >
     < RID : Replica | datastore: VS, AS' >
     {T, RID <- prepare(TID,VERSION,DC,A)}
   =>
     < TABLE : Table | datacenter: DR, AS >
     < RID : Replica | datastore: (VS,VERSION), AS' >
     [if inDC(RID,DC,DR) then ld else rd fi, A <- prepare-reply(TID,true,RID)] .  --- always "true" in RAMP

  op inDC : Address Address DcReplicas -> Bool .
  eq inDC(RID,DC,(DR ; dc(DC,(RID ; RIDS)))) = true .
  eq inDC(RID,DC,DR) = false [owise] .


 crl [receive-prepare-reply] :
     < TABLE : Table | table: REPLICA-TABLE, datacenter: DR >
     < A : Client | executing: < TID : Txn | operations: OPS,
	                                         txnSqn: SQN,
											 voteSites: VSTS,
											 commitSites: CMTS, AS >,
					dc: DC, AS' >
     {T, A <- prepare-reply(TID,FLAG,RID')}  --- FLAG is always true in RAMP
   =>
     < TABLE : Table | table: REPLICA-TABLE, datacenter: DR >
     if VSTS'[TID] == empty  --- all votes received
       then < A : Client | executing: < TID : Txn | operations: OPS,
	                                                txnSqn: SQN,
						                            voteSites: noVS,
						                            commitSites: (CMTS ; voteSites(TID,addVotes(OPS,REPLICA-TABLE,DR,DC))), AS >,
						   dc: DC, AS' >
            genCommits(A,TID,OPS,SQN,REPLICA-TABLE,DR,DC)
       else < A : Client | executing: < TID : Txn | operations: OPS,
	                                                txnSqn: SQN,
						                            voteSites: VSTS',
						                            commitSites: CMTS, AS >,                    
						   dc: DC, AS' >
     fi 
   if VSTS' := remove(TID,RID',VSTS) .
   

  op _[_] : VoteSites Address -> AddressSet .
  eq (voteSites(TID,RIDS) ; VSTS)[TID] = RIDS . 

  op remove : Address Address VoteSites -> VoteSites .
  eq remove(TID,RID,(voteSites(TID,(RID ; RIDS)) ; VSTS)) =
       voteSites(TID,RIDS) ; VSTS .
  eq remove(TID,RID,VSTS) = VSTS [owise] .
  
  op genCommits : Address Address OperationList Float ReplicaTable DcReplicas Address -> Config .
  eq genCommits(A,TID,(write(OID,K,EXPR) OPS),SQN,[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC) =
       genCommits(A,TID,OPS,SQN,[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC)
       [ld, RID <- commit(TID,ts(A,SQN),DC,A)] .
  eq genCommits(A,TID,((OID X :=read K) OPS),SQN,[KR],DR,DC) = genCommits(A,TID,OPS,SQN,[KR],DR,DC) .
  eq genCommits(A,TID,nil,SQN,[KR],DR,DC) = null .


  rl [receive-commit] :
     < TABLE : Table | datacenter: DR, AS >
     < RID : Replica | datastore: VS,
                       latestCommit: LC, AS' >
     {T, RID <- commit(TID,ts(A,SQN),DC,A)}
   =>
     < TABLE : Table | datacenter: DR, AS >
     < RID : Replica | datastore: VS,
                       latestCommit: cmt(LC,VS,ts(A,SQN)), AS' >
     [if inDC(RID,DC,DR) then ld else rd fi, A <- committed(TID,RID)] .
	 
	 
  op cmt : KeyTimestamps Versions Timestamp -> KeyTimestamps .
  eq cmt(LC,VS,TS) = $cmt(LC,keyFilter(VS,TS),TS) .
  op $cmt : KeyTimestamps KeySet Timestamp -> KeyTimestamps .
  eq $cmt(LC,(K,KS),TS) = $cmt(insert(K,maxts(LC,K,TS),LC),KS,TS) .
  eq $cmt(LC,empty,TS) = LC .
  
  op keyFilter : Versions Timestamp -> KeySet . 
  eq keyFilter((version(K,V,TS,MD),VS),TS) = K, keyFilter(VS,TS) .
  eq keyFilter(VS,TS) = empty [owise] .

  op maxts : KeyTimestamps Key Timestamp -> Timestamp .
  eq maxts(LC,K,TS) = if $hasMapping(LC,K)
                           then if LC[K] less-than TS
                                  then TS
                                  else LC[K]
                                fi
                           else TS
                         fi .

  op _less-than_ : Timestamp Timestamp -> Bool .
  eq ts(RID,SQN) less-than ts(RID',SQN') =
          if SQN < SQN'
            then true
            else if SQN == SQN' and RID < RID'
                   then true
                   else false
                 fi
          fi .


 crl [receive-committed] :
     < M : Monitor | log: (RECORD ; record(TID,T1,0.0,READS,WRITES,false) ; RECORD') >
	 < TABLE : Table | table: REPLICA-TABLE, datacenter: DR >
     < A : Client | executing: < TID : Txn | operations: OPS,
	                                         localVars: VARS,
	                                         txnSqn: SQN,
						                     commitSites: CMTS,
						 					 voteSites: VSTS, AS >,
                    committed: TXNS,
					dc: DC, AS' >
     {T, A <- committed(TID,RID')}
   =>
     < TABLE : Table | table: REPLICA-TABLE, datacenter: DR >
     if CMTS'[TID] == empty  --- all "committed" received
       then < M : Monitor | log: (RECORD ; record(TID,T1,T,READS,WRITES,true) ; RECORD') >
            < A : Client | executing: noActor,
                           committed: (TXNS ;; < TID : Txn | operations: OPS,
						                                     localVars: VARS,
						                                     txnSqn: SQN,
								                             commitSites: noVS,
								  	   				         voteSites: (VSTS ; addVotesForRest(TID,OPS,REPLICA-TABLE,DR,DC)), AS >),  
	   					   dc: DC, AS' >
            [nd, A <- start]  
			genPutsForRest(A,TID,OPS,SQN,VARS,REPLICA-TABLE,DR,DC)
       else < M : Monitor | log: (RECORD ; record(TID,T1,0.0,READS,WRITES,false) ; RECORD') >
            < A : Client | executing: < TID : Txn | operations: OPS,
			                                        localVars: VARS,
			                                        txnSqn: SQN,
						                            commitSites: CMTS',
						 	   					    voteSites: VSTS, AS >,
                           committed: TXNS,              
	   					   dc: DC, AS' >
     fi
     if CMTS' := remove(TID,RID',CMTS) .
  
  
  op addVotesForRest : Address OperationList ReplicaTable DcReplicas Address -> VoteSites .
  op $addVotesForRest : Address OperationList ReplicaTable DcReplicas Address VoteSites -> VoteSites .  
  eq addVotesForRest(TID,OPS,[KR],DR,DC) = $addVotesForRest(TID,OPS,[KR],DR,DC,noVS) .
  eq $addVotesForRest(TID,(write(OID,K,EXPR) OPS),[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC,VSTS) =
       $addVotesForRest(TID,OPS,[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC,addRestSites(TID,DR,(RIDSS ;; RIDSS'),VSTS)) .
  eq $addVotesForRest(TID,((OID X :=read K) OPS),[KR],DR,DC,VSTS) = $addVotesForRest(TID,OPS,[KR],DR,DC,VSTS) .
  eq $addVotesForRest(TID,nil,[KR],DR,DC,VSTS) = VSTS .  
  
  op addRestSites : Address DcReplicas AddressList VoteSites -> VoteSites .
  eq addRestSites(TID,(DR ; dc(DC,(RID ; RIDS))),(RID ;; RIDSS),(voteSites(TID,DC,RIDS') ; VSTS)) =
       addRestSites(TID,(DR ; dc(DC,(RID ; RIDS))),RIDSS,(voteSites(TID,DC,(RIDS' ; RID)) ; VSTS)) .
  eq addRestSites(TID,(DR ; dc(DC,(RID ; RIDS))),(RID ;; RIDSS),VSTS) = 
       addRestSites(TID,(DR ; dc(DC,(RID ; RIDS))),RIDSS,(voteSites(TID,DC,RID) ; VSTS)) [owise] .
  eq addRestSites(TID,DR,emptyAddrList,VSTS) = VSTS .
  
  op genPutsForRest : Address Address OperationList Float LocalVars ReplicaTable DcReplicas Address -> Config .	
  op $genPutsForRest : Address Address OperationList Float LocalVars ReplicaTable DcReplicas Address OperationList -> Config .	
  eq genPutsForRest(A,TID,OPS,SQN,VARS,[KR],DR,DC) = $genPutsForRest(A,TID,OPS,SQN,VARS,[KR],DR,DC,OPS) .
  eq $genPutsForRest(A,TID,(write(OID,K,EXPR) OPS),SQN,VARS,[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC,(OPS' write(OID,K,EXPR) OPS'')) =
       $genPutsForRest(A,TID,OPS,SQN,VARS,[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC,(OPS' write(OID,K,EXPR) OPS''))
	   $$genPutsForRest(prepare(TID,version(K,eval(EXPR,VARS),ts(A,SQN),md(OPS' OPS'')),DC,A),(RIDSS ;; RIDSS')) .
  eq $genPutsForRest(A,TID,((OID X :=read K) OPS),SQN,VARS,[KR],DR,DC,OPS') =
       $genPutsForRest(A,TID,OPS,SQN,VARS,[KR],DR,DC,OPS') .
  eq $genPutsForRest(A,TID,nil,SQN,VARS,[KR],DR,DC,OPS') = null .
  
  op $$genPutsForRest : Content AddressList -> Config .
  eq $$genPutsForRest(CONT,(RID ;; RIDSS)) = [rd,RID <- CONT] $$genPutsForRest(CONT,RIDSS) .
  eq $$genPutsForRest(CONT,emptyAddrList) = null .
  
 
 crl [receive-prepare-reply-committed] :
     < TABLE : Table | table: REPLICA-TABLE, datacenter: DR >
     < A : Client | committed: (TXNS ;; < TID : Txn | operations: OPS,
	                                                  txnSqn: SQN,
										              voteSites: VSTS,
											          commitSites: CMTS, AS > ;; TXNS'),
					dc: DC, AS' >
     {T, A <- prepare-reply(TID,FLAG,RID')}  --- FLAG is always true in RAMP
   =>
     < TABLE : Table | table: REPLICA-TABLE, datacenter: DR >
     if allReceivedFromSameDc(VSTS')  --- all votes received for some datacenter
       then < A : Client | committed: (TXNS ;; < TID : Txn | operations: OPS,
	                                                txnSqn: SQN,
						                            voteSites: onlyRestRemain(VSTS'),
						                            commitSites: (CMTS ; voteSites(TID,allReceivedFromWhichDc(VSTS'),addVotesForThatDc(OPS,REPLICA-TABLE,DR,VSTS'))), AS > ;; TXNS'),
						   dc: DC, AS' >
            genCommitsForThatDc(A,TID,OPS,SQN,REPLICA-TABLE,DR,DC,allReceivedFromWhichDc(VSTS'))
       else < A : Client | committed: (TXNS ;; < TID : Txn | operations: OPS,
	                                                txnSqn: SQN,
						                            voteSites: VSTS',
						                            commitSites: CMTS, AS > ;; TXNS'),                    
						   dc: DC, AS' >
     fi 
   if VSTS' := remove(TID,RID',VSTS) .
   
   
   op remove : Address Address VoteSites -> VoteSites .
   eq remove(TID,RID,(voteSites(TID,DC,(RID ; RIDS)) ; VSTS)) = 
        voteSites(TID,DC,RIDS) ; VSTS .
   eq remove(TID,RID,VSTS) = VSTS [owise] .
   
   op allReceivedFromSameDc : VoteSites -> Bool . 
   eq allReceivedFromSameDc(voteSites(TID,DC,empty) ; VSTS) = true .
   eq allReceivedFromSameDc(VSTS) = false [owise] .
   
   op onlyRestRemain : VoteSites -> VoteSites .
   eq onlyRestRemain(voteSites(TID,DC,empty) ; VSTS) = onlyRestRemain(VSTS) .
   eq onlyRestRemain(VSTS) = VSTS [owise] .
   
   op addVotesForThatDc : OperationList ReplicaTable DcReplicas VoteSites -> AddressSet . 
   op $addVotesForThatDc : OperationList ReplicaTable DcReplicas Address -> AddressSet .
   eq addVotesForThatDc(OPS,REPLICA-TABLE,DR,VSTS) = $addVotesForThatDc(OPS,REPLICA-TABLE,DR,allReceivedFromWhichDc(VSTS)) .
   eq $addVotesForThatDc((write(OID,K,EXPR) OPS),[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC) =
        RID ; $addVotesForThatDc(OPS,[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC) .
   eq $addVotesForThatDc(((OID X :=read K) OPS),[KR],DR,DC) = $addVotesForThatDc(OPS,[KR],DR,DC) .
   eq $addVotesForThatDc(nil,[KR],DR,DC) = empty .
   
   op allReceivedFromWhichDc : VoteSites -> Address .    
   eq allReceivedFromWhichDc(voteSites(TID,DC,empty) ; VSTS) = DC .
   
   op genCommitsForThatDc : Address Address OperationList Float ReplicaTable DcReplicas Address Address -> Config .
   eq genCommitsForThatDc(A,TID,(write(OID,K,EXPR) OPS),SQN,[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC',DC) =
        genCommitsForThatDc(A,TID,OPS,SQN,[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC',DC)
		[rd, RID <- commit(TID,ts(A,SQN),DC',A)] .
   eq genCommitsForThatDc(A,TID,((OID X :=read K) OPS),SQN,[KR],DR,DC',DC) =
		genCommitsForThatDc(A,TID,OPS,SQN,[KR],DR,DC',DC) .
   eq genCommitsForThatDc(A,TID,nil,SQN,[KR],DR,DC',DC) = null .
      

  rl [receive-committed-committed] :
     < A : Client | committed: (TXNS ;; < TID : Txn | commitSites: CMTS, AS > ;; TXNS'), AS' >
     {T, A <- committed(TID,RID')}
   =>
     < A : Client | committed: (TXNS ;; < TID : Txn | commitSites: onlyRestRemain(remove(TID,RID',CMTS)), AS > ;; TXNS'), AS' > .
   

  rl [receive-get] :
     < TABLE : Table | datacenter: DR, AS >
     < RID : Replica | datastore: VS,
                       latestCommit: LC, AS' >
     {T, RID <- get(TID,K,TS,DC,A)}
   =>
     < TABLE : Table | datacenter: DR, AS >
     < RID : Replica | datastore: VS,
                       latestCommit: LC, AS' >
     [if inDC(RID,DC,DR) then ld else rd fi, A <- 
       (if TS == eptTS then response1(TID,vmatch(K,VS,LC),RID) else response2(TID,vmatch(K,VS,TS),RID) fi)] .
		 

  op vmatch : Key Versions KeyTimestamps -> Version .
  op vmatch : Key Versions Timestamp -> Version .
  eq vmatch(K,(version(K,V,TS,MD),VS),(K |-> TS,LC)) = version(K,V,TS,MD) .
  eq vmatch(K,VS,LC) = version(K,[0],ts(0,0.0),empty) [owise] .  
  --- ??? deal with "K not in LC", namely, the first txn has reads
  eq vmatch(K,(version(K,V,TS,MD),VS),TS) = version(K,V,TS,MD) .
  eq vmatch(K,VS,TS) = version(K,[0],ts(0,0.0),empty) [owise] .  
  --- this should never apply since the 2nd-round read will fetch the version which has been prepared

  op lat : KeyTimestamps KeySet Timestamp -> KeyTimestamps .
  eq lat(VL,(K,MD),TS) = lat(insert(K,maxts(VL,K,TS),VL),MD,TS) .
  eq lat(VL,empty,TS) = VL .
		

 crl [receive-response1] :
     < M : Monitor | log: (RECORD ; record(TID,T1,0.0,READS,WRITES,false) ; RECORD') >
     < TABLE : Table | table: REPLICA-TABLE, datacenter: DR >
     < A : Client | executing: < TID : Txn | operations: (OPS (OID X :=read K) OPS'),
                                             readSet: RS,
                                             localVars: VARS,
                                             latest: VL,
						                     1stGetSites: 1STGETS,
						                     2ndGetSites: 2NDGETS, AS >,
					dc: DC, AS' >
     {T, A <- response1(TID,version(K,V,TS,MD),RID')}
   =>
     < M : Monitor | log: (RECORD ; record(TID,T1,0.0,(READS,version(K,V,TS,MD)),WRITES,false) ; RECORD') >
     < TABLE : Table | table: REPLICA-TABLE, datacenter: DR >
     if 1STGETS'[TID] == empty
       then < A : Client  | executing: < TID : Txn | operations: (OPS (OID X :=read K) OPS'),
                                                     readSet: RS',
                                                     localVars: insert(X,V,VARS),  --- useless for read-only txns
                                                     latest: VL',
						                             1stGetSites: 1STGETS',
						                             2ndGetSites: (2NDGETS ; 2nd(TID,RIDS)), AS >,
							dc: DC, AS' >
            gen2ndGets(A,TID,VL',RS',REPLICA-TABLE,DR,DC)
            [nd, A <- commit-reads]
       else < A : Client | executing: < TID : Txn | operations: (OPS (OID X :=read K) OPS'),
                                                    readSet: RS',
                                                    localVars: insert(X,V,VARS),  --- useless for read-only txns
                                                    latest: VL',
						                            1stGetSites: 1STGETS',
						                            2ndGetSites: 2NDGETS, AS >,
							dc: DC, AS' >  
     fi 
     if RS' := RS, version(K,V,TS,MD) /\
        VL' := lat(VL,MD,TS) /\
        1STGETS' := remove(TID,RID',1STGETS) /\
        RIDS := 2ndSites(lat(VL,MD,TS),(RS,version(K,V,TS,MD)),REPLICA-TABLE,DR,DC) . 
		

  op _[_] : 1stGets Address -> AddressSet .
  eq (1st(TID,RIDS) ; 1STGETS)[TID] = RIDS . 

  op _[_] : 2ndGets Address -> AddressSet .
  eq (2nd(TID,RIDS) ; 2NDGETS)[TID] = RIDS . 
  
  op remove : Address Address 1stGets -> 1stGets .
  eq remove(TID,RID,(1st(TID,(RID ; RIDS)) ; 1STGETS)) =
       1st(TID,RIDS) ; 1STGETS .
  eq remove(TID,RID,1STGETS) = 1STGETS [owise] .

  op remove : Address Address 2ndGets -> 2ndGets .
  eq remove(TID,RID,(2nd(TID,(RID ; RIDS)) ; 2NDGETS)) =
       2nd(TID,RIDS) ; 2NDGETS .
  eq remove(TID,RID,2NDGETS) = 2NDGETS [owise] .
		
  op 2ndSites : KeyTimestamps Versions ReplicaTable DcReplicas Address -> AddressSet .
  eq 2ndSites((K |-> TS',VL),(version(K,V,TS,MD),RS),[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC) =
       2ndSites((K |-> TS',VL),RS,[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC) ;
	   (if TS less-than TS' then RID else empty fi) .
  eq 2ndSites(VL,RS,[KR],DR,DC) = empty [owise] .  --- if K is not in VL
  eq 2ndSites(VL,empty,[KR],DR,DC) = empty .
		
  op gen2ndGets : Address Address KeyTimestamps Versions ReplicaTable DcReplicas Address -> Config .		
  eq gen2ndGets(A,TID,(K |-> TS',VL),(version(K,V,TS,MD),RS),[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC) = 
       gen2ndGets(A,TID,(K |-> TS',VL),RS,[replicatingSites(K,(RIDSS ;; RID ;; RIDSS')) ;; KR],(DR ; dc(DC,(RID ; RIDS))),DC)
	   (if TS less-than TS' then [ld, RID <- get(TID,K,TS',DC,A)] else null fi) .
  eq gen2ndGets(A,TID,VL,RS,[KR],DR,DC) = null [owise] .  --- if K is not in VL
  eq gen2ndGets(A,TID,VL,empty,[KR],DR,DC) = null .

  
    rl [receive-response2] :
     < M : Monitor | log: (RECORD ; record(TID,T1,0.0,(READS,version(K,V',TS',MD')),WRITES,false) ; RECORD') >
     < A : Client | executing: < TID : Txn | operations: (OPS (OID X :=read K) OPS'),
                                             readSet: (RS,version(K,V',TS',MD')),
                                             localVars: VARS,
								      		 2ndGetSites: 2NDGETS, AS >, AS' >
     {T, A <- response2(TID,version(K,V,TS,MD),RID')}
   => 
     < M : Monitor | log: (RECORD ; record(TID,T1,0.0,(READS,version(K,V,TS,MD)),WRITES,false) ; RECORD') >   
     < A : Client | executing: < TID : Txn | operations: (OPS (OID X :=read K) OPS'),
                                             readSet: (RS,version(K,V,TS,MD)),
                                             localVars: insert(X,V,VARS),
								     		 2ndGetSites: remove(TID,RID',2NDGETS), AS >, AS' >
     [nd, A <- commit-reads] .



  --- RAMP deals with rw-txns in a different way than Walter, 
  --- that is, all reads are sent out at the beginning of the txn
  --- so "writeSet" not needed: no read-your-write-in-the-same-txn
 crl [commit-reads] :
     {T, A <- commit-reads}
     < M : Monitor | log: (RECORD ; record(TID,T1,0.0,READS,WRITES,false) ; RECORD') > 
     < TABLE : Table | table: REPLICA-TABLE, datacenter: DR >
     < A : Client | executing: < TID : Txn | operations: OPS,
                                             localVars: VARS,
                                             readSet: RS,
						                     1stGetSites: 1STGETS,
						                     2ndGetSites: 2NDGETS,
						                     txnSqn: SQN,
						                     voteSites: VSTS, AS >,
                    committed: TXNS,
                    dc: DC, AS' >
   =>
     < TABLE : Table | table: REPLICA-TABLE, datacenter: DR >
     if read-only(OPS)
       then < M : Monitor | log: (RECORD ; record(TID,T1,T,READS,WRITES,true) ; RECORD') >
            < A : Client | executing: noActor,
                           committed: (TXNS ;; < TID : Txn | operations: OPS,
                                                             localVars: VARS,
                                                             readSet: RS,
			      			                                 1stGetSites: delete(TID,1STGETS),
								                             2ndGetSites: delete(TID,2NDGETS),
								                             txnSqn: SQN,
								                             voteSites: VSTS, AS >),
                           dc: DC, AS' >
            [nd, A <- start] 
       else < M : Monitor | log: (RECORD ; record(TID,T1,0.0,READS,WRITES',false) ; RECORD') >
            < A : Client | executing: < TID : Txn | operations: OPS,
                                                    localVars: VARS,
                                                    readSet: RS,
						                            1stGetSites: delete(TID,1STGETS),
						                            2ndGetSites: delete(TID,2NDGETS),
						                            txnSqn: T,
						                            voteSites: (VSTS ; voteSites(TID,addVotes(OPS,REPLICA-TABLE,DR,DC))), AS >,
                            committed: TXNS,
                            dc: DC, AS' >
            genPuts(A,TID,OPS,T,VARS,REPLICA-TABLE,DR,DC)
     fi 
     if 1STGETS[TID] == empty /\
        2NDGETS[TID] == empty /\
        WRITES' := ws(OPS,A,T,VARS) .

  op read-only : OperationList -> Bool .
  eq read-only((OPS write(OID,K,EXPR) OPS')) = false .
  eq read-only(OPS) = true [owise] .

  op delete : Address 1stGets -> 1stGets .
  eq delete(TID,(1st(TID,RIDS) ; 1STGETS)) = 1STGETS .
  eq delete(TID,1STGETS) = 1STGETS [owise] .

  op delete : Address 2ndGets -> 2ndGets .
  eq delete(TID,(2nd(TID,RIDS) ; 2NDGETS)) = 2NDGETS .
  eq delete(TID,2NDGETS) = 2NDGETS [owise] .
  

 crl [not-ready-to-commit-reads] :
     {T, A <- commit-reads}
     < A : Client | executing: < TID : Txn | 1stGetSites: 1STGETS,
                                             2ndGetSites: 2NDGETS, AS >, AS' >
  =>
     < A : Client | executing: < TID : Txn | 1stGetSites: 1STGETS,
                                             2ndGetSites: 2NDGETS, AS >, AS' >
     if 1STGETS[TID] =/= empty or 2NDGETS[TID] =/= empty .
    
endm
